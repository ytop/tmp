/**
 * Copyright 2020 Huawei Technologies Co., Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#include "runtime/device/gpu/cuda_common.h"
#include "include/cuda_fp16.h"

#include "backend/kernel_compiler/gpu/cuda_impl/topk_lib.cuh"
#include "backend/kernel_compiler/gpu/cuda_impl/argmaxwithvalue_impl.cuh"
#include <limits>
#include <algorithm>

const int kWarpGroup = 4;
const int kBlockSize = 512;
const int kGroupSize = kWarpGroup * kWarpSize;
const int kNumWarps = kBlockSize / kWarpSize;

template <typename T, typename S>
__global__ void Warp4ArgMax(int outer_size, int bound, int inner_size, const T *input, T *output, S *output_index) {
  __shared__ T shared_K[kNumWarps];
  __shared__ S shared_V[kNumWarps];
  const T init_K = static_cast<T>(-9999);
  const S init_V = static_cast<S>(-1);

  for (int t_idx = blockIdx.x * blockDim.x + threadIdx.x; t_idx < kGroupSize * outer_size * inner_size;
       t_idx += blockDim.x * gridDim.x) {
    int outer_id = t_idx / kGroupSize / inner_size;
    int inner_id = t_idx / kGroupSize % inner_size;

    int groupId = threadIdx.x / kGroupSize;
    int tgId = threadIdx.x % kGroupSize;
    int warpId = threadIdx.x / kWarpSize;
    int laneId = threadIdx.x % kWarpSize;

    T threadK = init_K;
    S threadV = init_V;

    if (laneId == 0) {
      shared_K[warpId] = init_K;
      shared_V[warpId] = init_V;
    }
    __syncthreads();

    for (int i = tgId; i < bound; i += kGroupSize) {
      T other_K = input[outer_id * bound * inner_size + i * inner_size + inner_id];
      S other_V = i;
      bool is_bigger = Cmp<T>::lt(threadK, other_K);
      ConditionAssign(is_bigger, &threadK, other_K);
      ConditionAssign(is_bigger, &threadV, other_V);
    }
    __syncwarp();

    for (int offset = kWarpSize / 2; offset > 0; offset /= 2) {
      T other_K = __shfl_down_sync(0xffffffff, threadK, offset);
      S other_V = __shfl_down_sync(0xffffffff, threadV, offset);

      bool is_bigger = Cmp<T>::lt(threadK, other_K);
      ConditionAssign(is_bigger, &threadK, other_K);
      ConditionAssign(is_bigger, &threadV, other_V);
    }

    __syncwarp();

    if (laneId == 0) {
      shared_K[warpId] = threadK;
      shared_V[warpId] = threadV;
    }
    __syncthreads();

    if (tgId < 22) {
      bool is_bigger = Cmp<T>::lt(shared_K[(groupId * kWarpGroup) + tgId], shared_K[(groupId * kWarpGroup) + tgId + 2]);
      ConditionAssign(is_bigger, (shared_K + (groupId * kWarpGroup) + tgId),
                      (shared_K[(groupId * kWarpGroup) + tgId + 2]));
      ConditionAssign(is_bigger, (shared_V + (groupId * kWarpGroup) + tgId),
                      (shared_V[(groupId * kWarpGroup) + tgId + 2]));
    }
    __syncwarp();

    if (tgId == 0) {
      bool is_bigger = Cmp<T>::lt(shared_K[(groupId * kWarpGroup) + tgId], shared_K[(groupId * kWarpGroup) + tgId + 1]);
      ConditionAssign(is_bigger, (shared_K + (groupId * kWarpGroup) + tgId),
                      (shared_K[(groupId * kWarpGroup) + tgId + 1]));
      ConditionAssign(is_bigger, (shared_V + (groupId * kWarpGroup) + tgId),
                      (shared_V[(groupId * kWarpGroup) + tgId + 1]));

      // The first thread of each group write output
      output[outer_id * inner_size + inner_id] = shared_K[groupId * kWarpGroup];
      output_index[outer_id * inner_size + inner_id] = shared_V[groupId * kWarpGroup];
    }
    __syncthreads();
  }
}

template <typename T, typename S>
void ArgmaxWithValue(int outer_size, int bound, int inner_size, const T *input, T *output, S *output_index,
                     cudaStream_t stream) {
  int block_num_limit = outer_size * inner_size * kGroupSize;
  Warp4ArgMax<T, S><<<GET_BLOCKS(block_num_limit), kBlockSize, 0, stream>>>(outer_size, bound, inner_size, input,
                                                                            output, output_index);
}

template <typename T, typename S>
void CalArgmaxWithValue(const T *input, const int bound, const int outerSize, const int innerSize, S *index, T *output,
                        cudaStream_t cuda_stream) {
  ArgmaxWithValue(outerSize, bound, innerSize, input, output, index, cuda_stream);
  return;
}

template void CalArgmaxWithValue<float, int>(const float *input, const int bound_, const int outerSize_,
                                             const int innerSize_, int *index, float *output, cudaStream_t cuda_stream);
template void CalArgmaxWithValue<half, int>(const half *input, const int bound_, const int outerSize_,
                                            const int innerSize_, int *index, half *output, cudaStream_t cuda_stream);
